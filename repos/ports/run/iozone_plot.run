#
# \brief  IOzone data plotting
# \author Emery Hemingway
# \date   2017-07-27
#

set timeout -1

set datadir var/data/iozone
set datafiles [glob $datadir/*.out]

spawn gnuplot

send "load \"${genode_dir}/repos/ports/run/iozone.dem\"\n"

proc plot {title column} {
	global datafiles

	send "clear\n"
	send "set title \"$title\"\n"
	send "splot "
	foreach datafile $datafiles {
		set name [lindex [split [lindex [split $datafile /] end] . ] 0]
		set tmpfile $name.$title.data

		# work around the messy escaping logic
		set args {$1 ~ /^[0-9]+/ }
		append args "\{"
		append args { print  $1 " " $2 " " $}
		append args "$column \}"

		exec awk $args $datafile > /tmp/$tmpfile

		send "\"/tmp/$tmpfile\" title \"$name\", "
	}
	send "\npause mouse button3 \"Press the right mouse button to continue\"\n"
}

plot write 3
plot rewrite 4
plot read 5
plot reread 6
plot randread 7
plot randwrite 8
plot bkwdread 9
plot recrewrite 10
plot strideread 11
plot fwrite 12
plot frewrite 13
plot fread 14
plot freread 15

expect "nothing"
